# Edit on github: https://github.com/LizardByte/Sunshine/tree/nightly/packaging/linux/aur/PKGBUILD
# Reference: https://wiki.archlinux.org/title/PKGBUILD

pkgname='sunshine'
pkgver=v0.21.0
pkgrel=1
pkgdesc="Sunshine is a self-hosted game stream host for Moonlight."
arch=('x86_64' 'aarch64')
url=https://app.lizardbyte.dev/Sunshine
license=('GPL3')

depends=('avahi'
         'boost-libs'
         'curl'
         'libayatana-appindicator'
         'libevdev'
         'libmfx'
         'libnotify'
         'libpulse'
         'libva'
         'libvdpau'
         'libx11'
         'libxcb'
         'libxfixes'
         'libxrandr'
         'libxtst'
         'numactl'
         'openssl'
         'opus'
         'udev')
makedepends=('boost'
             'cmake'
             'git'
             'make'
             'nodejs'
             'npm')
optdepends=('cuda: NvFBC capture support'
            'libcap'
            'libdrm')

provides=('sunshine')

source=("$pkgname::git+https://github.com/LizardByte/Sunshine.git#commit=5bca024899eff8f50e04c1723aeca25fc5e542ca")
sha256sums=('SKIP')

prepare() {
    cd "$pkgname"
    # Skip submodules that we don't want
    if [[ $CARCH == "x86_64" ]]; then
        git -c submodule."ffmpeg-macos-x86_64".update=none \
            -c submodule."ffmpeg-windows-x86_64".update=none \
            -c submodule."ffmpeg-linux-aarch64".update=none \
            -c submodule."ffmpeg-macos-aarch64".update=none \
            submodule update --recursive --init
    elif [[ $CARCH == "aarch64" ]]; then
        git -c submodule."ffmpeg-macos-x86_64".update=none \
            -c submodule."ffmpeg-windows-x86_64".update=none \
            -c submodule."ffmpeg-linux-x86_64".update=none \
            -c submodule."ffmpeg-macos-aarch64".update=none \
            submodule update --recursive --init

    # It's unlikely that someone could get this far on a system with an incorrect arch, but we should handle it anyway
    # Pull linux ffmpeg submodules
    else
        git -c submodule."ffmpeg-macos-x86_64".update=none \
            -c submodule."ffmpeg-windows-x86_64".update=none \
            -c submodule."ffmpeg-macos-aarch64".update=none \
            submodule update --recursive --init
    fi
}

build() {
    pushd "$pkgname"
    npm install
    popd

    export BRANCH="master"
    export BUILD_VERSION="v0.21.0"
    export COMMIT="5bca024899eff8f50e04c1723aeca25fc5e542ca"

    export CFLAGS="${CFLAGS/-Werror=format-security/}"
    export CXXFLAGS="${CXXFLAGS/-Werror=format-security/}"

    cmake \
        -S "$pkgname" \
        -B build \
        -Wno-dev \
        -D CMAKE_INSTALL_PREFIX=/usr \
        -D SUNSHINE_EXECUTABLE_PATH=/usr/bin/sunshine \
        -D SUNSHINE_ASSETS_DIR="share/sunshine"

    make -C build
}

package() {
    make -C build install DESTDIR="$pkgdir"
}
